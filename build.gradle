// ============================================================================
// (C) Copyright Schalk W. Cronje 2013-2014
//
// This software is licensed under the Apache License 2.0
// See http://www.apache.org/licenses/LICENSE-2.0 for license details
//
// Unless required by applicable law or agreed to in writing, software distributed under the License is
// distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and limitations under the License.
//
// ============================================================================

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
      }
      dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.2'
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1'
        classpath "com.netflix.nebula:gradle-extra-configurations-plugin:2.2.2"
      }
}

allprojects {
    ext {
        versionModifier = ''
        versionNumber   = '1.0.1'
        modulesWithGroovyDoc = [
            'dsl',
            'gradle-plugin',
            'cloud-provider-core',
            'smb-provider'
        ]
    }
    version = versionNumber + (versionModifier.size() ? "-${versionModifier}": '')
}

subprojects {

    buildscript {
        repositories {
            jcenter()
            mavenLocal()
        }
        dependencies {
            classpath 'org.ysb33r.gradle:bintray:1.6'
            classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:3.0.1'
        }
    }

    ext {
        vfsVersion       = '2.1'
        groovyVer        = '[2.1,2.3.9]'
        jackrabbitVer    = '1.6.5' // '2.11.1'
        jettyVer         = '8.1.5.v20120716' //'9.3.5.v20151012'
        slf4jVer         = '1.7.5'
        bintrayRepo      = 'grysb33r'
        bintrayUser      = 'ysb33r'
        bintrayLicense   = 'Apache-2.0'
        bintrayAttributes= [:]
        websitePublishFolder = new File(rootProject.projectDir,'website')
    }

    repositories {
        jcenter()
    }

}

configure(subprojects.findAll { ! ['jlan','test-servers','docs'].contains(it.name) } ) {

    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'com.github.hierynomus.license'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    plugins.withType(JavaPlugin) {

        project.tasks.withType(JavaCompile) { task ->
            task.sourceCompatibility = project.sourceCompatibility
            task.targetCompatibility = project.targetCompatibility
        }

        project.tasks.withType(GroovyCompile) { task ->
            task.sourceCompatibility = project.sourceCompatibility
            task.targetCompatibility = project.targetCompatibility
        }
    }

    dependencies {
        compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7+'
        compile ("org.apache.commons:commons-vfs2:${vfsVersion}") {
            exclude group:'org.apache.maven.scm'
        }
        compile 'org.apache.commons:commons-compress:1.9'
        testCompile 'org.apache.ftpserver:ftpserver-core:1.0.6'
        testCompile 'commons-io:commons-io:2.4'
        testCompile 'commons-net:commons-net:3.+'
        testRuntime 'commons-httpclient:commons-httpclient:3.1'
        testRuntime "org.slf4j:slf4j-simple:${slf4jVer}"


        testCompile ('org.spockframework:spock-core:1.0-groovy-2.3') {
            exclude module : 'groovy-all'
        }

    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        description "An archive of the JavaDocs for Maven Central"
        classifier "javadoc"
        from javadoc
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    license {
        header = rootProject.file('config/apache-header')
        strictCheck = true
        ignoreFailures = false
        mapping {
            groovy ='SLASHSTAR_STYLE'
        }
        ext.year = '2013-2015'
        excludes(['**/*.ad', '**/*.asciidoc', '**/*.adoc', '**/*.md','**/*.properties', '**/*.txt', '**/*.bz2'])
    }
}

configure( subprojects.findAll { modulesWithGroovyDoc.contains(it.name) } ) {
    task installDocs( type : Copy ) {
        group 'documentation'
        description 'Copy groovydocs to an install directory'
        dependsOn 'groovydoc'

        from groovydoc

        into { "${websitePublishFolder}/${version}/api/${project.name}" }

        dependsOn groovydoc
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

